#! /bin/zsh
# manydots - zle tweak for emulating "..."=="../.." etc.
# Credit - https://github.com/knu/zsh-manydots-magic

# Copyright (c) 2011, 2012 Akinori MUSHA
# Licensed under the 2-clause BSD license.
#
# This tweak helps input ancestor directories beyond the parent (`..')
# in a handy way.  You can just type triple dots to input `../..',
# quadruple dots to `../../..', etc..

manydots.self-insert() {
  emulate -L zsh
  local self_insert_function magic_count
  zstyle -s ':manydots' self-insert-function self_insert_function

  if [[ "$KEYS" == .* && "$LBUFFER" != *...* && "$LBUFFER" == *.. ]] && {
    local -a words
    words=("${(@Q)${(z)LBUFFER}}")
    # `...` is a wildcard operator in go
    [[ ${${(@)words[1,-2]}[(I)go]} = 0 ]] &&
    [[ $words[-1] == (|*[/=]|[\<\>=]\().. ]]
  }
  then
    [[ "$LASTWIDGET" == (self-insert|backward-delete-char) ]] &&
    zstyle -s ':manydots' magic-count magic_count
    zstyle ':manydots' magic-count $((magic_count+1))
    LBUFFER="$LBUFFER/."
    zle "$self_insert_function"
    return
  fi

  # cancel expansion if it does not seem right
  if [[ "$KEYS" != [=/,:\;\|\&\<\>\(\)\[\]{}^~\'\"\`[:space:]]* &&
    "$LASTWIDGET" == (self-insert|backward-delete-char) && "$LBUFFER" == *../.. ]] && {
    zstyle -s ':manydots' magic-count magic_count
    [[ "$magic_count" -gt 0 ]]
  }
  then
    repeat $magic_count LBUFFER="${LBUFFER%/..}"
    repeat $magic_count LBUFFER="$LBUFFER."
  fi

  zstyle ':manydots' magic-count 0

  zle "$self_insert_function"
}

manydots.backward-delete-char() {
    emulate -L zsh
    local backward_delete_char_function
    zstyle -s ':manydots' backward-delete-char-function backward_delete_char_function

    if [[ "$LASTWIDGET" == (self-insert|backward-delete-char) && "$LBUFFER" == *../.. ]] && {
      local magic_count
      zstyle -s ':manydots' magic-count magic_count
      [[ "$magic_count" -gt 0 ]]
    }
    then
      zstyle ':manydots' magic-count $((magic_count-1))
      LBUFFER="${LBUFFER%..}"
    else
      zstyle ':manydots' magic-count 0
    fi

    zle "$backward_delete_char_function"
}

manydots.on() {
  emulate -L zsh
  local self_insert_function="${$(zle -lL | awk \
    '$1=="zle"&&$2=="-N"&&$3=="self-insert"{print $4;exit}'):-.self-insert}"

  [[ "$self_insert_function" == manydots.self-insert ]] && return 0

  # For url-quote-magic which does not zle -N itself
  zle -la "$self_insert_function" || zle -N "$self_insert_function"

  zstyle ':manydots' self-insert-function "$self_insert_function"

  zle -A manydots.self-insert self-insert

  local backward_delete_char_function="$(zle -lL | awk \
    '$1=="zle"&&$2=="-N"&&$3=="backward-delete-char"{print $4;exit}')"

  if [[ -n "$backward_delete_char_function" ]]
  then
    zle -la "$backward_delete_char_function" || zle -N "$backward_delete_char_function"
  else
    zle -A backward-delete-char manydots.orig.backward-delete-char
    backward_delete_char_function=manydots.orig.backward-delete-char
  fi

  zstyle ':manydots' backward-delete-char-function "$backward_delete_char_function"

  zle -A manydots.backward-delete-char backward-delete-char

  zstyle ':manydots' magic-count 0

  return 0
}

manydots.off() {
  emulate -L zsh
  local self_insert_function backward_delete_char_function
  zstyle -s ':manydots' self-insert-function self_insert_function

  [[ -n "$self_insert_function" ]] &&
    zle -A "$self_insert_function" self-insert

  zstyle -s ':manydots' backward-delete-char-function backward_delete_char_function

  [[ -n "$backward_delete_char_function" ]] &&
    zle -A "$backward_delete_char_function" backward-delete-char

  zstyle ':manydots' magic-count 0

  return 0
}

zle -N manydots.self-insert
zle -N manydots.backward-delete-char
zle -N manydots.on
zle -N manydots.off

manydots() {
  manydots.on
}

[[ -o kshautoload ]] || manydots "$@"
