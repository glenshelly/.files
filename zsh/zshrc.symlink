# setup

# path to dotfiles
export DOTFILES=$HOME/.files

# source prompt
# need to set theme before
# sourcing oh my zsh
source $DOTFILES/zsh/prompt.zsh

# source oh-my-zsh
plugins=( git )
export ZSH="$HOME/.oh-my-zsh"
source $ZSH/oh-my-zsh.sh

# all zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load !path && !completion files
# don't double load fpath file
# (sourced earlier for prompt)
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/fpath.zsh}
do
  source $file
done

# start autocomplete
autoload -Uz compinit
compinit

# load completion files
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done


unset config_files
