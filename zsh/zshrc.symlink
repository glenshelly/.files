# <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
# This zshrc sources files in $DOTFILES/zsh & any `.zsh` files in the directory
# specified by "$DOTFILES" (set to `$HOME/.files` in `./zshenv.symlink`)
# <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

# <<<< files without dependencies >>>>

# store tokens/keys in `secrets.zsh`, gitignored to avoid checking in
[ -f "$DOTFILES/zsh/secrets.zsh" ] && source "$DOTFILES/zsh/secrets.zsh"

# this should be one of the first files sourced, see explanation at the top of
# the file
source "$DOTFILES/zsh/instant_prompt.zsh"

source "$DOTFILES/zsh/completion.zsh"
source "$DOTFILES/zsh/config.zsh"
source "$DOTFILES/zsh/keymap.zsh"
source "$DOTFILES/zsh/manydots.zsh"
source "$DOTFILES/zsh/options.zsh"

# <<<< files with dependencies >>>>
# note: some files here also define dependencies for later files so the ordering
# in this section matters (unlike in the previous two sections)

source "$DOTFILES/zsh/functions.zsh"

# source after $DOTFILES/zsh/functions.zsh to allow aliasing those functions
source "$DOTFILES/zsh/alias.zsh"

# load and configure plugins
source "$DOTFILES/zsh/plugins.zsh"
# configure prompt after enabling
source "$DOTFILES/zsh/prompt.zsh"

# define variable to store list of files to source
# note: -U ensures uniqueness
typeset -U config_files

# all *.zsh files throughout this repo exluding the $DOTFILES/zsh &
# $DOTFILES/submodules directories (technically excludes $DOTFILES/**/zsh &
# $DOTFILES/**/submodules however the only matching directories are those two)
#
# x~y matches anything that matches the pattern x but does not match y
# ref - http://zsh.sourceforge.net/Doc/Release/Expansion.html#Glob-Operators
# (search "x~y" to find documentation for the above described behavior)
config_files=($DOTFILES/**/*.zsh~*/zsh/*~*/submodules/*)

# $config_files referenced in this file
source "$DOTFILES/zsh/path.zsh"

# `.zsh` files that are not `path.zsh`
typeset -u non_path_files
# `:#<pattern>` removes any strings that match the given pattern
# ref - http://zsh.sourceforge.net/Doc/Release/Expansion.html#Parameter-Expansion
# (search "(M)" to find the relevant documentation (though note this is NOT
# actually using the "(M)" flag here))
non_path_files=(${config_files:#*/path.zsh})
for non_path_file in $non_path_files
do
  source "$non_path_file"
done
unset non_path_files non_path_file

unset config_files

# source asdf to give it final say on package versions
source "$DOTFILES/zsh/asdf.zsh"
