# setup

# path to dotfiles
export DOTFILES=$HOME/.files

# source prompt
# need to set theme before
# sourcing oh my zsh
source $DOTFILES/zsh/prompt.zsh

# source oh-my-zsh
# if installed @ ~/.oh-my-zsh
if [ -d $HOME/.oh-my-zsh ]; then
  plugins=( git )
  export ZSH="$HOME/.oh-my-zsh"
  source $ZSH/oh-my-zsh.sh
fi

# load functions written as basic scripts
autoload -Uz $DOTFILES/functions/*(:t)

# all zsh files
typeset -U config_files

# enable `extended_glob` to pattern match & exclude `submodules` directory
# disable immediately after to respect options in `options.zsh`
# TODO: avoid `extended_glob` hack
# must be some way to exclude `submodules` directory w/o `extended_glob` set
setopt extended_glob
config_files=($DOTFILES/**/*.zsh~*submodules*)
setopt noextended_glob

# load path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load !path && !completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# after setting fpath
# load functions (such as manydots)
# that need to be run to take effect
_execute_functions

# start autocomplete
autoload -Uz compinit

# use cache for compinit
# TODO: profile this to see how much of a difference it actually makes
# ref - https://blog.callstack.io/supercharge-your-terminal-with-zsh-8b369d689770
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load completion files
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
